// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Sys.jack

/**
 * A library that supports various program execution services.
 */
class Sys {

    /** Performs all the initializations required by the OS. */
    function void init() {
        do Math.init();
        do Memory.init();
        do Screen.init();
        do Output.init();
        do Keyboard.init();
        // String and Array has no init function
        do Main.main();
    }

    /** Halts the program execution. */
    function void halt() {
        while(true) {}
        return;
    }

    /** Waits approximately duration milliseconds and returns.  */
    // wait is a machine-dependent
    function void wait(int duration) {
        var int factor;
        while(duration > 0) {
            let duration = duration - 1;
            let factor = 100;
            while(factor > 0) {
                let factor = factor - 1;
            }
        }
        return;
    }

    /** Displays the given error code in the form "ERR<errorCode>",
     *  and halts the program's execution. */
    function void error(int errorCode) {
        do Output.printString("ERR<");
        do Output.printInt(errorCode);
        do Output.printString(">");
        do Output.println();
        do Sys.halt();
        return;
    }
}
