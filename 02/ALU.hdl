// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs,
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN
        x[16], y[16],  // 16-bit inputs
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    Mux16(a=x, b=false, sel=zx, out=rzx);
    Not16(in=rzx, out=notrzx);
    Mux16(a=rzx, b=notrzx, sel=nx, out=rnx);

    Mux16(a=y, b=false, sel=zy, out=rzy);
    Not16(in=rzy, out=notrzy);
    Mux16(a=rzy, b=notrzy, sel=ny, out=rny);

    Add16(a=rnx, b=rny, out=fadd);
    And16(a=rnx, b=rny, out=fand);
    Mux16(a=fand, b=fadd, sel=f, out=resultf);

    Not16(in=resultf, out=notresultf);
    Mux16(a=resultf, b=notresultf, sel=no, out=tout, out[0..7]=w1, out[8..15]=w2, out[15]=leftmost);

    // if out = 0 then zr = 1, else zr = 0
    Or8Way(in=w1, out=o1);
    Or8Way(in=w2, out=o2);
    Or(a=o1, b=o2, out=o3);
    Mux(a=true, b=false, sel=o3, out=zr);

    // if out<0 then ng = 1 else ng = 0
    Mux(a=false, b=true, sel=leftmost, out=ng);

    And16(a=tout, b=tout, out=out);
}
